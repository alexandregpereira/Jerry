apply plugin: 'maven-publish'

def githubProperties = new Properties()
githubProperties.load(
        new FileInputStream(rootProject.file("github.properties"))
)

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.findProperty("library.groupId")
                artifactId = project.findProperty("library.artifactId")
                version = project.findProperty("library.version") + versionSuffix()

                artifact("$buildDir/outputs/aar/jerry-release.aar")
                artifact androidSourcesJar
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                def repository = project.findProperty("library.repository")
                url = uri("https://maven.pkg.github.com/alexandregpereira/" + repository)
                credentials {
                    username = githubProperties['github.user'] ?: System.getenv("GITHUB_USER")
                    password = githubProperties['github.userToken'] ?: System.getenv("GITHUB_USER_TOKEN")
                }
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from android.sourceSets.main.java.srcDirs
}

static def versionSuffix() {
    def sufix = System.getenv("VERSION_SUFIX") ?: ''
    if (sufix != '') {
        sufix = "$sufix-${new Date().format('yyyyMMddHHmmss')}"
    }
    return sufix
}